import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Dialogs

Dialog {
    id: hub
    objectName: "exportHub"
    required property var backend
    required property var renderer
    required property string markdown
    required property url documentBaseUrl
    required property bool darkMode
    property string selectedFormat: "pdf"
    property string cssName: backend.outputCssName()
    property string cssFeedback: ""
    property int previewLayoutMode: 2
    property bool compact: width < 900
    signal destinationRequested(string format)
    Connections {
        target: hub.backend
        function onOutputCssChanged() { hub.cssName = hub.backend.outputCssName() }
    }
    FileDialog {
        id: cssPicker
        title: "Choose a local CSS file for HTML output"
        nameFilters: ["CSS files (*.css)"]
        fileMode: FileDialog.OpenFile
        onAccepted: {
            if (hub.backend.loadOutputCss(selectedFile))
                hub.cssFeedback = "CSS applied to HTML export only."
            else
                hub.cssFeedback = hub.backend.status
        }
    }
    component SecondaryAction: Button {
        id: action
        font.pixelSize: 13
        font.weight: Font.DemiBold
        leftPadding: 14; rightPadding: 14
        topPadding: 7; bottomPadding: 7
        background: Rectangle {
            radius: 7
            color: action.down || action.hovered ? backend.palette.hover : backend.palette.field
            border.color: action.visualFocus ? backend.palette.focus
                : action.hovered || action.down ? backend.themeAccent : backend.palette.border
        }
        contentItem: Label {
            text: action.text; font: action.font; color: action.enabled ? backend.palette.text : backend.palette.muted
            horizontalAlignment: Text.AlignHCenter; verticalAlignment: Text.AlignVCenter
        }
    }

    title: "Export and share"
    modal: true
    focus: true
    closePolicy: Popup.CloseOnEscape
    anchors.centerIn: parent
    width: Math.max(0, Math.min(1100, parent.width - 32))
    height: Math.max(0, Math.min(720, parent.height - 32))
    padding: 0
    standardButtons: Dialog.NoButton
    background: Rectangle { color: backend.palette.panel; border.color: backend.palette.border; radius: 12 }
    header: Rectangle {
        implicitHeight: 76; color: "transparent"
        Label {
            x: 22; y: 14; text: hub.title
            color: backend.palette.text; font.pixelSize: 20; font.weight: Font.DemiBold
            Accessible.role: Accessible.Heading
        }
        Label {
            x: 22; y: 44; width: parent.width - 44
            text: "Choose a format and style, then save or share."
            elide: Text.ElideRight
            color: backend.palette.muted; font.pixelSize: 13
        }
        Rectangle { anchors.bottom: parent.bottom; width: parent.width; height: 1; color: backend.palette.border }
    }
    contentItem: ColumnLayout {
        spacing: 0
        RowLayout {
            Layout.fillWidth: true; Layout.fillHeight: true; Layout.margins: 20; spacing: 18
            ScrollView {
                id: optionsScroll
                Layout.preferredWidth: hub.compact ? -1 : 250
                Layout.fillWidth: hub.compact; Layout.fillHeight: true
                clip: true; rightPadding: 10; contentWidth: availableWidth
                ScrollBar.horizontal.policy: ScrollBar.AlwaysOff
                ScrollBar.vertical: ScrollBar {
                    id: optionsBar
                    parent: optionsScroll
                    x: optionsScroll.width - width - 2
                    y: 0
                    height: optionsScroll.height
                    policy: ScrollBar.AsNeeded
                    implicitWidth: 7
                    background: Item {}
                    contentItem: Rectangle { radius: 3; color: backend.palette.muted; opacity: optionsBar.active ? 0.7 : 0.35 }
                }
                ColumnLayout {
                width: optionsScroll.availableWidth; spacing: 12
                Label { text: "Output"; color: backend.palette.text; font.pixelSize: 14; font.weight: Font.DemiBold }
                ComboBox {
                    objectName: "exportFormatChoice"; Layout.fillWidth: true
                    model: [{ label: "PDF document", value: "pdf" }, { label: "HTML document", value: "html" }]
                    textRole: "label"; valueRole: "value"; currentIndex: hub.selectedFormat === "html" ? 1 : 0
                    onActivated: hub.selectedFormat = currentValue; Accessible.name: "Export format"
                }
                Label { text: "Selected style"; color: backend.palette.text; font.pixelSize: 14; font.weight: Font.DemiBold }
                Label { Layout.fillWidth: true; text: backend.outputTemplateName + " · " + backend.outputFont + " · " + backend.outputPointSize + " pt"; wrapMode: Text.Wrap; color: backend.palette.muted; font.pixelSize: 13; Accessible.name: "Current output style: " + text }
                StyleGallery { objectName: "exportCompactGallery"; visible: hub.compact; Layout.fillWidth: true; Layout.preferredHeight: 280; backend: hub.backend; onStyleChanged: hub.forceActiveFocus() }
                ColumnLayout {
                    visible: hub.selectedFormat === "html"
                    Layout.fillWidth: true; spacing: 4
                    Label { text: "Custom HTML CSS: " + hub.cssName; Layout.fillWidth: true; elide: Text.ElideMiddle; color: backend.palette.muted; font.pixelSize: 11; Accessible.name: text }
                    RowLayout {
                        Layout.fillWidth: true
                        SecondaryAction { text: "Choose CSS…"; onClicked: cssPicker.open(); Accessible.name: "Choose local CSS for HTML export" }
                        SecondaryAction { text: "Clear"; enabled: hub.cssName !== "None"; onClicked: { backend.clearOutputCss(); hub.cssFeedback = "" }
                            Accessible.name: "Clear HTML CSS" }
                    }
                    Label { visible: hub.cssFeedback.length > 0; text: hub.cssFeedback; Layout.fillWidth: true; wrapMode: Text.Wrap; color: backend.palette.muted; font.pixelSize: 11 }
                }
                Label { text: "Paper"; color: backend.palette.text; font.pixelSize: 14; font.weight: Font.DemiBold }
                ComboBox {
                    objectName: "exportPaperChoice"; Layout.fillWidth: true
                    model: [{ label: "A4", value: "a4" }, { label: "US Letter", value: "letter" }, { label: "US Legal", value: "legal" }, { label: "A5", value: "a5" }]
                    textRole: "label"; valueRole: "value"
                    currentIndex: { for (var i = 0; i < model.length; ++i) if (model[i].value === backend.exportPaperSize()) return i; return 0; }
                    onActivated: backend.setExportPaperSize(currentValue); Accessible.name: "Paper size"
                }
                Label { text: "Orientation"; color: backend.palette.text; font.pixelSize: 14; font.weight: Font.DemiBold }
                RowLayout {
                    Layout.fillWidth: true
                    ButtonGroup { id: orientationGroup }
                    RadioButton { text: "Portrait"; ButtonGroup.group: orientationGroup; checked: backend.exportOrientation() === "portrait"; onClicked: backend.setExportOrientation("portrait"); Accessible.name: "Portrait orientation" }
                    RadioButton { text: "Landscape"; ButtonGroup.group: orientationGroup; checked: backend.exportOrientation() === "landscape"; onClicked: backend.setExportOrientation("landscape"); Accessible.name: "Landscape orientation" }
                }
                Label { Layout.fillWidth: true; text: selectedFormat === "html" ? "HTML uses the selected output style. Paper settings apply when printing it." : "PDF uses the selected style, paper size and orientation."; wrapMode: Text.Wrap; color: backend.palette.muted; font.pixelSize: 12 }
                SecondaryAction { visible: hub.compact; Layout.fillWidth: true; text: "Open paginated preview…"; onClicked: backend.printPreview(); Accessible.name: "Open paginated print preview" }
                SecondaryAction { visible: hub.width < 520; Layout.fillWidth: true; text: "Share Markdown…"; onClicked: backend.nativeWindowAction("share"); Accessible.name: "Share Markdown" }
                }
            }
            Rectangle { visible: !hub.compact; Layout.fillHeight: true; Layout.preferredWidth: 1; color: backend.palette.border }
            StyleGallery { objectName: "exportWideGallery"; visible: !hub.compact; Layout.preferredWidth: 280; Layout.fillHeight: true; backend: hub.backend; onStyleChanged: hub.forceActiveFocus() }
            Rectangle { visible: !hub.compact; Layout.fillHeight: true; Layout.preferredWidth: 1; color: backend.palette.border }
            ColumnLayout {
                visible: !hub.compact; Layout.fillWidth: true; Layout.fillHeight: true; spacing: 8
                Label { text: "Live output preview"; color: backend.palette.text; font.pixelSize: 14; font.weight: Font.DemiBold }
                Rectangle {
                    Layout.fillWidth: true; Layout.fillHeight: true; color: backend.palette.page; border.color: backend.palette.border; radius: 5; clip: true
                    RowLayout {
                        anchors.fill: parent
                        spacing: 0
                        ScrollView {
                            id: sourcePreviewScroll
                            visible: hub.previewLayoutMode === 1
                            Layout.fillWidth: true; Layout.fillHeight: true
                            clip: true
                            ScrollBar.horizontal.policy: ScrollBar.AlwaysOff
                            TextArea {
                                objectName: "exportHubSourcePreview"
                                width: sourcePreviewScroll.availableWidth
                                text: hub.markdown
                                readOnly: true
                                wrapMode: TextEdit.Wrap
                                selectByMouse: true
                                font.family: "Menlo"
                                font.pixelSize: 13
                                color: backend.palette.text
                                background: Rectangle { color: backend.palette.panel }
                                Accessible.name: "Markdown source for export preview"
                            }
                        }
                        Rectangle { visible: hub.previewLayoutMode === 1; Layout.fillHeight: true; Layout.preferredWidth: 1; color: backend.palette.border }
                        PreviewPane { objectName: "exportHubPreviewPane"; visualEditorObjectName: "exportHubVisualEditor"; allowVisualEdit: false; tonalLayoutButtons: true; Layout.fillWidth: true; Layout.fillHeight: true; renderer: hub.renderer; markdown: hub.markdown; documentBaseUrl: hub.documentBaseUrl; darkMode: hub.darkMode; typeface: backend.outputFont; textSize: backend.outputPointSize; layoutMode: hub.previewLayoutMode; onLayoutRequested: function(mode) { hub.previewLayoutMode = mode }; onLinkRequested: function(link) {} }
                    }
                }
                Label { Layout.fillWidth: true; text: "Continuous preview of the current output style. Custom HTML CSS and exact page breaks appear only in saved output or paginated preview."; wrapMode: Text.Wrap; color: backend.palette.muted; font.pixelSize: 12 }
                SecondaryAction { objectName: "exportPaginatedPreviewButton"; text: "Open paginated preview…"; onClicked: backend.printPreview(); Accessible.name: "Open paginated print preview" }
            }
        }
        Rectangle { Layout.fillWidth: true; Layout.preferredHeight: 1; color: backend.palette.border }
        RowLayout {
            Layout.fillWidth: true; Layout.margins: 20
            SecondaryAction { objectName: "exportShareMarkdownButton"; visible: hub.width >= 520; text: "Share Markdown…"; onClicked: backend.nativeWindowAction("share"); Accessible.name: "Share Markdown" }
            Item { Layout.fillWidth: true }
            SecondaryAction { text: "Cancel"; onClicked: hub.close(); Accessible.name: "Cancel export" }
            Button {
                id: saveAction
                objectName: "exportDestinationButton"
                text: selectedFormat === "pdf" ? "Save PDF…" : "Save HTML…"
                font.weight: Font.DemiBold
                leftPadding: 18; rightPadding: 18
                background: Rectangle {
                    radius: 7
                    color: saveAction.down ? "#204b7e" : saveAction.hovered ? "#3170b8" : "#285e9e"
                }
                contentItem: Text {
                    text: saveAction.text; font: saveAction.font; color: "white"
                    horizontalAlignment: Text.AlignHCenter; verticalAlignment: Text.AlignVCenter
                }
                onClicked: { hub.destinationRequested(hub.selectedFormat); hub.close(); }
                Accessible.name: text
            }
        }
    }
}
